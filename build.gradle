plugins {
    id 'java'
    // Plugin for jpackage (jdk14+) (see https://github.com/petr-panteleyev/jpackage-gradle-plugin)
    id "org.panteleyev.jpackageplugin" version "1.3.1"
}

group 'org.terra.incognita'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    // Using JDK 16, only for fun. Waiting for LTS version 17.
    // Using toolchain to auto select jdk version.
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

compileJava {
    // Using picocli to provide a command line interface to this program (see https://picocli.info/)
    options.compilerArgs += ["-Aproject=${rootProject.group}/${rootProject.name}"]
}

// Using jpackage to create an installation package (Linux, windows, macos)
// Prepare all files needed for the package
// First: dependnecies
task copyDependencies(type:Copy) {
    from(configurations.runtimeClasspath)
    into("${buildDir}/jars")
}

// Next: project jar file
task copyJar(type:Copy) {
    dependsOn("jar")
    from tasks.jar
    into "${buildDir}/jars"
}

// Create package, SO depend, to install program
jpackage {
    dependsOn(':build','copyDependencies','copyJar')
    type = "deb"


    appName = "FlyOnTheWallCatcher"
    appDescription = "A Java MitM detection tool for https"

    appVersion = project.version.toString()
    vendor = "org.terra.incognita"
    copyright = "Copyright (c) 2021 David PÃ©rez Serrada <dserrada at gmail dot com>"

    mainJar = "${jar.archiveFileName.get()}"
    mainClass = "org.terra.incognita.fotwc.cli.Main"

    input  = "$buildDir/jars"
    destination = "$buildDir/dist"
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'

    // Command line utility
    implementation 'info.picocli:picocli:4.6.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'

}

test {
    useJUnitPlatform()
}